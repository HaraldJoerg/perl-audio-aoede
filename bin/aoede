#!/usr/bin/env perl
use 5.032;
use strict;
use feature "signatures";
no warnings "experimental";

use Prima;
use Prima::Application name => 'Sound from Scratch';
use PDL::Graphics::Prima;
use Scalar::Util qw( refaddr );
use FindBin;
use lib "$FindBin::Bin/../lib";

use Audio::Aoede;
use Audio::Aoede::Envelope::ADSR;
use Audio::Aoede::Server;
use Audio::Aoede::Player::SoX;
use Audio::Aoede::Source;
use Audio::Aoede::Harmonics;
use Audio::Aoede::UI::Envelope;
use Audio::Aoede::UI::Oscilloscope;
use Audio::Aoede::UI::Register;
use Audio::Aoede::UI::Tuner;
use Audio::Aoede::Units qw( symbol );

######################################################################
# Configuration hardwired right now
my $tick = 1/60;  # How far can we go?
my $rate = 48000; # we have ~800 samples per second
my $bits = 16;    # Where should this be defined?
my $channels = 1; # Where should this be defined?
my $max_frequency = 22000;
my %timer_objects;

my $aoede = Audio::Aoede->new(
    rate => $rate,
);
my $server = Audio::Aoede::Server->new(
    rate => $rate,
);

my $harmonic;
my $combined;

my $main = Prima::MainWindow->create(
    text  => 'Sound from Scratch',
    backColor => cl::White,
    # Prima::Menu has a description of these formats
    menuItems =>
    [
        ['~File' =>
         [
            ['~Quit' => 'Ctrl+Q', '^Q',
             sub { $::application->close } ],
        ],
     ],
        ['~View' =>
         [
             ['@', # Empty Identifier with "auto-toggle" indicator
              '~Oscilloscope', # What's shown with keyboard shortcut
              #  We'll use that for "open file" later, so just for testing
              'Ctrl-O',        # Accelerator text
              '^O',            # The actual hotkey representation
              sub { toggle_oscilloscope() },
          ]
         ]
     ]
    ],
);

my $controls_widget = $main->insert(
    Widget =>
    pack => { side => 'top', fill => 'both' },
);

my $timer = $::application->insert(
    Timer =>
    timeout => 1000 * $tick,
    onTick  => sub { tick() }
);

my $player = Audio::Aoede::Player::SoX->new(
    rate	=> $rate,
    # encoding	=> $encoding,
    bits	=> $bits,
    channels	=> $channels,
    source      => $server,
);
$player->connect($server);

my $oscilloscope;

my $register = Audio::Aoede::UI::Register->new();
$register->insert_to($main);

my $tuner = Audio::Aoede::UI::Tuner->new();
$tuner->insert_to($main);

my $envelope = Audio::Aoede::Envelope::ADSR->new(
    attack => 1/500,
    decay  => 0,
    sustain => 1,
    release => 1/500,
);
my $envelope_ui = Audio::Aoede::UI::Envelope->new(
    trigger => sub (%adsr) {
        $envelope = Audio::Aoede::Envelope::ADSR->new(%adsr);
        reset_sources();
    }
);
$envelope_ui->insert_to($main);

my $key = $main->insert(
    Button =>
    pack => { side => 'top' },
    text => 'Play',
    hotkey => ' ',
    onMouseDown => sub {
        $server->add_sources($combined);
        $player->unmute;
    },
    onMouseUp => sub {
        $combined->shutdown;
    },
    onKeyDown => sub ($widget,$code,@rest) {
        return unless defined $code;
        if (chr $code  eq  ' ') {
            $combined->set_volume(1.0);
            $server->add_sources($combined);
            $player->unmute;
        }
    },
    onKeyUp => sub ($widget,$code,@rest) {
        return unless defined $code;
        if (chr $code  eq  ' ') {
            $combined->shutdown;
        }
    },
);


my $speaker =  $controls_widget->insert(
    Button =>
    checkable => 1,
    enabled => 0,
    text => symbol("LOOP"),
    onClick => sub ($widget) {
        if ($widget->checked) {
            $widget->text(symbol("MUTE"));
            $key->enabled(0);
            $player->unmute;
            $player->update;
            reset_sources();
            $server->add_sources($harmonic);
        }
        else {
            $widget->text(symbol("LOOP"));
            $key->enabled(1);
            $player->update;
            $player->mute;
            $server->set_sources();
        }
    },
    pack => { side => 'left', fill => 'both' },
);


reset_sources();
$speaker->enabled(1);
register_timer($player);
$player->start;
if ($oscilloscope) {
    register_timer($oscilloscope);
}

sub reset_sources {
    my $frequency = $tuner->value;
    if ($frequency) {
        $harmonic = Audio::Aoede::Harmonics->new(
            rate => $rate,
            frequency => $frequency,
            volumes => [$register->volumes],
        );
        $combined = Audio::Aoede::Source->new(
            rate => $rate,
            function => sub ($n_samples,$rate,$first) {
                my $samples = $harmonic->next_samples($n_samples,$first);
            },
            effects => [$envelope],
        );
    }
    else {
        $server->set_sources();
    }
}


$register->set_trigger(sub { $harmonic->set_volumes($register->volumes) });
$tuner->set_trigger(sub {
                        $harmonic  or  return;
                        $harmonic->set_frequency($tuner->value // 0)
                    } );

my $o_window;
sub toggle_oscilloscope {
    if ($o_window) {
        $o_window->destroy; # also unregisters the oscilloscope
        undef $o_window;
        undef $oscilloscope;
        return;
    }
    $o_window = $main->insert(
        Window =>
        parent => $main,
        size => [400,400],
        text => 'Aoede Oscilloscope',
        onClose => sub { undef $o_window },
        onDestroy => sub { unregister_timer($oscilloscope) },
    );
    $oscilloscope = Audio::Aoede::UI::Oscilloscope->new(
        parent    => $o_window,
        frequency => 440,
        rate      => $rate,
        source    => $server,
        size      => [400,400],
        pack      => { side => 'top', fill => 'both' },
    );
    register_timer($oscilloscope);
}


sub tick {
    for (values %timer_objects) {
        $_->update();
    }
}

sub register_timer ($object) {
    if (! scalar keys %timer_objects) {
        $timer->start;
    }
    $timer_objects{refaddr $object} = $object;
}

sub unregister_timer ($object) {
    delete $timer_objects{refaddr $object};
    if (! scalar keys %timer_objects) {
        $timer->stop;
    }
}


Prima->run;
