# ABSTRACT: A parser for MRT files
package Audio::Aoede::MusicRoll::Parser; # for the tools

use 5.032;
use utf8;                       # for the unicode MUSICAL SYMBOL stuff
use warnings;
use feature 'signatures';
no warnings 'experimental';
use autodie;                    # being lazy :)
use Feature::Compat::Class;

use Carp;

use Audio::Aoede::MusicRoll;
use Audio::Aoede::MusicRoll::Section;
use Audio::Aoede::Note;
use Audio::Aoede::Units qw( A440 HALFTONE );

my %diatonic_notes = (
    C => 0,   D => 2,   E => 4,   F => 5,   G => 7,   A => 9,   B => 11,
);
my %diatonic_modifiers = (
    ''  =>  0,
    'b' => -1,   '‚ô≠' => -1,
    '#' =>  1,   '‚ôØ' =>  1,
);
my %note_symbols = (
    'ùÖù' => 1,           # U+1D15D MUSICAL SYMBOL WHOLE NOTE
    'ùÖû' => 1/2,         # U+1D15E MUSICAL SYMBOL HALF NOTE
    'ùÖü' => 1/4,         # U+1D15F MUSICAL SYMBOL QUARTER NOTE
    'ùÖ†' => 1/8,         # U+1D160 MUSICAL SYMBOL EIGHTH NOTE
    'ùÖ°' => 1/16,        # U+1D161 MUSICAL SYMBOL SIXTEENTH NOTE
    'ùÖ¢' => 1/32,        # U+1D162 MUSICAL SYMBOL THIRTY-SECOND NOTE
    'ùÖ£' => 1/64,        # U+1D163 MUSICAL SYMBOL SIXTY-FOURTH NOTE
    'ùÖ§' => 1/128,       # U+1D164 M. S. ONE HUNDRED TWENTY-EIGHTH NOTE
);
my $note_symbol_pattern = '[' . join('', keys %note_symbols) . ']';
my $note_dot = 'ùÖ≠'; # U+1D16D MUSICAL SYMBOL COMBINING AUGMENTATION DOT

my %rest_symbols = (
    'ùÑª' => 1,           # U+1D13B MUSICAL SYMBOL WHOLE REST
    'ùÑº' => 1/2,         # U+1D13C MUSICAL SYMBOL HALF REST
    'ùÑΩ' => 1/4,         # U+1D13D MUSICAL SYMBOL QUARTER REST
    'ùÑæ' => 1/8,         # U+1D13E MUSICAL SYMBOL EIGHTH REST
    'ùÑø' => 1/16,        # U+1D13F MUSICAL SYMBOL SIXTEENTH REST
    'ùÖÄ' => 1/32,        # U+1D140 MUSICAL SYMBOL THIRTY-SECOND REST
    'ùÖÅ' => 1/64,        # U+1D141 MUSICAL SYMBOL SIXTY-FOURTH REST
    'ùÖÇ' => 1/128,       # U+1D142 M. S. ONE HUNDRED TWENTY-EIGHTH REST
);
my $rest_symbol_pattern = '[' . join('', keys %rest_symbols) . ']';

my %key_map = (
    'ùÖü' => 'bpm'
);

my $note_pattern =
    qr{
          ^
          (?:                 # durations come in two flavors
              (?<symbol>$note_symbol_pattern) (?<dot>$note_dot?)
          |
              (?<digits>(?&DIGITS))
          )
          :                   # Separates pitch from duration
          (?<notes>(?&NOTE)(?:\+(?&NOTE))*)
      |
          (?:                 # Rests also come in two flavors
              (?<rest_symbol>$rest_symbol_pattern)
          |
              (?<digits>(?&DIGITS))
              :
              (?<base>R)
          )
          $
          (?(DEFINE)
              (?<DIGITS>[0-9\/.]+) # 1/4 or 0.5
              (?<NOTE>
                  [A-G]       # The base note name
                  [b‚ô≠#‚ôØ]?     # up or down half a note, Unicode or ASCII
                  (?:[\d]|-1) # We don't support the tenth octave
              )
          )
          #          |
          #              (?<bar>[|ùÑÄ])
          #          |
          #              (?<repeat_start> \|\|: | ùÑÜ )
          #          |
          #              (?<repeat_end> :\|\| | ùÑá )
  }ix;
my $comment_pattern = qr{ ^ \# }x;
my $command_pattern = qr{ ^ ! (?<command>\w+) \s+ (?<params>.*) }x;

sub parse_file ($path) {
    my %default_params = (bpm => 120, tracks => 1);
    my %params = %default_params;
    my @tracks;
    my $current_track = 0;
    my $music_roll = Audio::Aoede::MusicRoll->new();
    open (my $score, '<:encoding(UTF-8)', $path);
  LINE:
    while (my $line = <$score>) {
        next LINE if ($line !~ /\S/);
        next LINE if ($line =~ /$comment_pattern/);
        $line =~ /$command_pattern/  and  do {
            $+{command} eq 'set'  and  do {
                # We have a new section, so close the old one
                $current_track = 0;
                if (@tracks) {
                    $music_roll->add_section(
                        Audio::Aoede::MusicRoll::Section->new(
                            bpm => $params{bpm},
                            tracks => [@tracks],
                        )
                    );
                }
                my @settings = split /\s*;\s*/,$+{params};
                %params = (%default_params, map {
                    my ($key,$value) = split /\s*=\s*/,$_,2;
                    $key = $key_map{$key} // $key;
                    ($key,$value);
                } @settings);
                @tracks = (map { [] } (1..$params{tracks}));
            };
            next LINE;
        };
        # Now we know we have a notes line
        my @tokens = split /\s+/, $line;
        my @notes = map {
            my $note_string = $_;
            my $note;
            $note_string =~ /$note_pattern/  and  do {
                my $duration;
                if (my $digits = $+{digits}) {
                    $duration = eval $digits;
                } else {
                    if (my $symbol = $+{symbol}) {
                        $duration = $note_symbols{$symbol};
                    } elsif (my $rest = $+{rest_symbol}) {
                        $duration = $rest_symbols{$rest};
                    } else {
                        croak "Invalid score: No duration in '$note_string'";
                    }
                    if ($+{dot}) {
                        $duration *= 1.5;
                    }
                }
                if ($+{notes}) {
                    my @notes = split /\+/,$+{notes};
                    my @pitches = map {
                        m{(?<base>[A-G])
                          (?<modifier>[b‚ô≠#‚ôØ]?) #
                          (?<octave>[\d]|-1)
                     }ix;
                        my $number = $diatonic_notes{$+{base}}
                            + $diatonic_modifiers{$+{modifier}}
                            + ($+{octave}+1) * 12;
                        my $pitch = 2 * A440 * (HALFTONE**($number-69));
                    } @notes;
                    $note = Audio::Aoede::Note->new(
                        duration => $duration,
                        pitches  => \@pitches,
                    );
                } else {
                    # No note => Treat it as a rest
                    $note = Audio::Aoede::Note->new(
                        duration => $duration,
                    );
                }
            };
            $note // ();
        } @tokens;
        push $tracks[$current_track]->@*,@notes;
        $current_track += 1;
        $current_track %= $params{tracks};
    }
  FINALIZE:                  # All lines read, process current section
    $music_roll->add_section(
        Audio::Aoede::MusicRoll::Section->new(
            bpm => $params{bpm},
            tracks => [@tracks],
        )
    );
    return $music_roll;
}

1;

__END__

=encoding utf8

=head1 NAME

Audio::Aoede::MusicRoll::Parser - parse MRT files

=head1 SYNOPSIS

  use Audio::Aoede::MusicRoll::Parser;
  my $music_roll = Audio::Aoede::MusicRoll::Parser::parse_file($path);

=head1 DESCRIPTION

Parse a L<MRT file|Audio::Aoede::MusicRoll::Format> into a
L<Audio::Aoede::MusicRoll> object.

=head1 SUBROUTINES

This module has only one subroutine.

=over

=item C<< $music_roll = parse_file($path) >>

Read a file from C<$path> and create a L<Audio::Aoede::MusicRoll>
object from its contents.

Dies when it sees stuff it does not understand.  The error messages
are not very helpful, I'm afraid.

=back

=head1 AUTHOR

Harald J√∂rg, E<lt>haj@posteo.deE<gt>

=head1 COPYRIGHT AND LICENSE

Copyright 2024 Harald J√∂rg

This module is free software; you may redistribute it and/or modify it
under the same terms as Perl itself.
